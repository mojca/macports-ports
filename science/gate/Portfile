# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

# Keep in sync with Geant4 (and/or implement a PortGroup)

PortSystem          1.0
PortGroup           app 1.0
PortGroup           cmake 1.0
PortGroup           github 1.0
PortGroup           active_variants 1.1
PortGroup           compiler_blacklist_versions 1.0

compiler.whitelist  macports-clang-3.9

# set git_sha         0be152cf
# set git_date        20151007

# github.setup        OpenGATE Gate ${git_sha}
github.setup        OpenGATE Gate 8.0 v
name                gate
# version             8.0
# version           7.1-git-${git_date}-${git_sha}

categories          science
maintainers         {mojca @mojca} openmaintainer
license             LGPL
description         GATE description
long_description    GATE is dedicated to numerical simulations in medical imaging and radiotherapy. \
                    It currently supports simulations of Emission Tomography (PET and SPECT), \
                    Computed Tomography (CT) and Radiotherapy experiments.
homepage            http://www.opengatecollaboration.org
platforms           darwin

checksums           rmd160  3628bb0b490915d71756457fd2d1715ab411695e \
                    sha256  40c4313832085fd2e5b08050241db174ef73036902fec0032ec0f225eaf779b6

# TODO: take this data from a geant4 portgroup
set geant.data_versions_10.3 {
    G4NDL              4.5   G4NDL                G4NEUTRONHPDATA
    G4EMLOW            6.50  G4EMLOW              G4LEDATA
    PhotonEvaporation  4.3   G4PhotonEvaporation  G4LEVELGAMMADATA
    RadioactiveDecay   5.1   G4RadioactiveDecay   G4RADIOACTIVEDATA
    G4NEUTRONXS        1.4   G4NEUTRONXS          G4NEUTRONXSDATA
    G4PII              1.3   G4PII                G4PIIDATA
    RealSurface        1.0   RealSurface          G4REALSURFACEDATA
    G4SAIDDATA         1.1   G4SAIDDATA           G4SAIDXSDATA
    G4ABLA             3.0   G4ABLA               G4ABLADATA
    G4ENSDFSTATE       2.1   G4ENSDFSTATE         G4ENSDFSTATEDATA
    G4TENDL            1.3   G4TENDL              G4TENDL
}
set geant.data_versions_10.2 {
    G4NDL              4.5   G4NDL                G4NEUTRONHPDATA
    G4EMLOW            6.48  G4EMLOW              G4LEDATA
    PhotonEvaporation  3.2   G4PhotonEvaporation  G4LEVELGAMMADATA
    RadioactiveDecay   4.3.2 G4RadioactiveDecay   G4RADIOACTIVEDATA
    G4NEUTRONXS        1.4   G4NEUTRONXS          G4NEUTRONXSDATA
    G4PII              1.3   G4PII                G4PIIDATA
    RealSurface        1.0   RealSurface          G4REALSURFACEDATA
    G4SAIDDATA         1.1   G4SAIDDATA           G4SAIDXSDATA
    G4ABLA             3.0   G4ABLA               G4ABLADATA
    G4ENSDFSTATE       1.2.3 G4ENSDFSTATE         G4ENSDFSTATEDATA
}

set geant.datadir ""
set geant.data_versions ""

app.name            Gate
app.executable      Gate
app.icon            ${filespath}/gate.png

cmake.out_of_source yes

depends_lib         port:root6

platform darwin {
    if { ${configure.cxx_stdlib} ne "libc++" } {
        configure.cxx_stdlib libc++
        configure.args-append -DCMAKE_POLICY_DEFAULT_CMP0025=NEW
        depends_lib-append port:libcxx
    }
}

patchfiles-append   patch-addmenu.diff

configure.args-append \
                    -DGATE_USE_OPTICAL=ON \
                    -DGATE_USE_SYSTEM_CLHEP=OFF \
                    -DROOT_CONFIG_EXECUTABLE=${prefix}/libexec/root6/bin/root-config
                    # ROOT is optional: we could make a variant if needed

pre-destroot {
    set destdocdir ${destroot}${prefix}/share/doc/${name}
    xinstall -m 755 -d ${destdocdir}
    # copy license
    copy ${worksrcpath}/license ${destdocdir}
    # copy materials database
    set sharedir ${prefix}/share/${name}
    set destsharedir ${destroot}${sharedir}
    xinstall -m 755 -d ${destsharedir}
    copy ${worksrcpath}/GateMaterials.db ${destsharedir}
    copy ${worksrcpath}/GateMaterialsGPU.db ${destsharedir}
    # copy the initial macro files with basic setup
    copy ${filespath}/gui.mac   ${destsharedir}
    copy ${filespath}/icons.mac ${destsharedir}
}

post-destroot {
    # TODO: the share folder is already set in another phase and should not repeat (actually, all the pre-destroot actions could go to post-destroot)
    set sharedir ${prefix}/share/${name}
    # move the binary and set environmental variables
    set destexecutable ${destroot}${prefix}/bin/Gate
    set libexecdir ${prefix}/libexec/${name}
    set destlibexecdir ${destroot}${libexecdir}
    xinstall -m 755 -d ${destlibexecdir}
    move ${destexecutable} ${destlibexecdir}
    system "echo '#!/bin/bash' > ${destexecutable}"
    system "echo export GATEHOME=\\\"${sharedir}\\\" >> ${destexecutable}"
    system "echo export G4DATADIR=\\\"${geant.datadir}\\\" >> ${destexecutable}"
    foreach {data.name data.version data.filename data.envvariable} ${geant.data_versions} {
        system "echo export ${data.envvariable}=\\\"\\\$G4DATADIR/${data.name}${data.version}\\\" >> ${destexecutable}"
    }
    system "echo ${libexecdir}/Gate \\\$@ >> ${destexecutable}"
    system "chmod 755 ${destexecutable}"
}

variant geant4102 conflicts geant4103 description {Use Geant4 10.0 (deprecated)} {
    set geant.version       10.2
    set geant.revision      3
    set geant.port_name     geant4.${geant.version}
    set geant.data_versions ${geant.data_versions_10.2}
    set geant.datadir       ${prefix}/share/Geant4/Data/Geant4.${geant.version}
    set geant.cmakedir      ${prefix}/lib/Geant4-${geant.version}.${geant.revision}

    depends_lib-append      port:${geant.port_name}
    configure.args-append   -DGeant4_DIR=${geant.cmakedir}
}
variant geant4103 conflicts geant4102 description {Use Geant4 10.1} {
    set geant.version       10.3
    set geant.revision      1
    set geant.port_name     geant4.${geant.version}
    set geant.data_versions ${geant.data_versions_10.3}
    set geant.datadir       ${prefix}/share/Geant4/Data/Geant4.${geant.version}
    set geant.cmakedir      ${prefix}/lib/Geant4-${geant.version}.${geant.revision}

    depends_lib-append      port:${geant.port_name}
    configure.args-append   -DGeant4_DIR=${geant.cmakedir}
}

variant qt4 conflicts qt5 description {Use Geant4 with Qt 4} {
    PortGroup qt4 1.0
}
variant qt5 conflicts qt4 description {Use Geant4 with Qt 5 (buggy)} {
    PortGroup qt5 1.0
}

if {![variant_isset geant4102] && ![variant_isset geant4103]} {
    default_variants-append +geant4103
}

# some ugly code to make sure that the variants for Qt version used
# are consistent between Gate and Geant4; looking for suggestions for improvement
if {[variant_isset geant4102]} {
    if {![catch {set result [active_variants geant4.10.2 qt5 qt4]}]} {
        if {$result} {
            default_variants-append +qt5
        } else {
            default_variants-append +qt4
        }
    }
    if {[variant_isset qt4]} {
        require_active_variants geant4.10.2 qt
    } elseif {[variant_isset qt5]} {
        require_active_variants geant4.10.2 qt5
    }
} elseif {[variant_isset geant4103]} {
    if {![catch {set result [active_variants geant4.10.3 qt5 qt4]}]} {
        if {$result} {
            default_variants-append +qt5
        } else {
            default_variants-append +qt4
        }
    }
    if {[variant_isset qt4]} {
        require_active_variants geant4.10.3 qt
    } elseif {[variant_isset qt5]} {
        require_active_variants geant4.10.3 qt5
    }
}

livecheck.version   ${version}
livecheck.regex     archive/\[v\](\[^"\]+)${extract.suffix}
